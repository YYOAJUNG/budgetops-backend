plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.5'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'co.uzzu.dotenv.gradle' version '4.0.0'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
description = 'Budgetops-backend'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
	maven { url 'https://jitpack.io' }
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0'
	implementation 'software.amazon.awssdk:sts:2.25.65'
	implementation 'software.amazon.awssdk:ec2:2.25.65'
	implementation 'software.amazon.awssdk:cloudwatch:2.25.65'
	implementation 'software.amazon.awssdk:costexplorer:2.25.65'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	runtimeOnly("org.postgresql:postgresql:42.7.4")

	// Lombok
	compileOnly 'org.projectlombok:lombok:1.18.34'
	annotationProcessor 'org.projectlombok:lombok:1.18.34'
	testCompileOnly 'org.projectlombok:lombok:1.18.34'
	testAnnotationProcessor 'org.projectlombok:lombok:1.18.34'

	// H2 (local 프로필에서 사용)
	runtimeOnly 'com.h2database:h2:2.3.232'
  
  // GCP 자격증명 및 API
	implementation 'com.google.auth:google-auth-library-oauth2-http:1.40.0'
	implementation 'com.google.cloud:google-cloud-bigquery:2.38.0'
	implementation 'com.google.cloud:google-cloud-monitoring:3.22.0'
  implementation 'com.google.apis:google-api-services-iam:v1-rev20251010-2.0.0'
	implementation 'com.google.cloud:google-cloud-asset:3.82.0'

	// Iamport (PortOne) 결제
	implementation 'com.github.iamport:iamport-rest-client-java:0.2.21'

	// JWT
	implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'
	
	// YAML 파싱
	implementation 'org.yaml:snakeyaml:2.2'
	
	// HTTP Client for Gemini API
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
}

tasks.named('test') {
	useJUnitPlatform()
}

// .env 파일의 환경변수를 bootRun 태스크에 자동으로 로드
// application-local.yml과 충돌하지 않도록 필요한 환경변수만 로드
tasks.named('bootRun') {
	doFirst {
		def dotenvFile = file('.env')
		if (dotenvFile.exists()) {
			// 로드할 환경변수 목록 (Spring 설정과 충돌하지 않는 것만)
			def allowedKeys = ['GOOGLE_CLIENT_ID', 'GOOGLE_CLIENT_SECRET', 'JWT_SECRET', 'APP_CRED_KEY']

			dotenvFile.readLines().each { line ->
				if (line && !line.startsWith('#') && line.contains('=')) {
					def (key, value) = line.split('=', 2)
					def trimmedKey = key.trim()
					if (allowedKeys.contains(trimmedKey)) {
						environment trimmedKey, value.trim()
					}
				}
			}
		}
	}
}
